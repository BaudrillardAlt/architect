#!/usr/bin/env fish

if not set -q NVIM_LISTEN_ADDRESS
    echo "NVIM_LISTEN_ADDRESS not set" >&2
    command nvim $argv
    exit
end

if not test -S "$NVIM_LISTEN_ADDRESS"
    if pgrep -x neovide >/dev/null
        hyprctl dispatch focuswindow class:neovide
        exit
    end
    if test (count $argv) -eq 1; and test -d "$argv[1]"
        set abs (realpath -m -- $argv[1])
        xx "$abs"
    else
        command app2unit /usr/share/applications/neovide.desktop -- --listen "$NVIM_LISTEN_ADDRESS" $argv
        while not hyprctl clients | grep -q 'class: neovide'
            sleep 0.1
        end
        hyprctl dispatch layoutmsg swapwithmaster
    end
    exit
end

if test (count $argv) -eq 0
    xx
    exit
end

for f in $argv
    if test -e "$f"
        set abs (realpath -- $f)
    else
        set abs (realpath -m -- $f)
    end

    if test -d "$abs"
        xx "$abs"
    else
        nvim --server $NVIM_LISTEN_ADDRESS --remote $abs
        hyprctl dispatch focuswindow class:neovide
    end
end
